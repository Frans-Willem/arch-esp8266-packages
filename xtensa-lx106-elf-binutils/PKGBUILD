# $Id$
# Maintainer: Frans-Willem Hardijzer <fw@hardijzer.nl>

_target=xtensa-lx106-elf
pkgname=$_target-binutils
pkgver=2.25.1
pkgrel=1
_commit=2bd25930
pkgdesc='A set of programs to assemble and manipulate binary and object files for the XTensa LX106 target'
arch=(i686 x86_64)
url='http://www.gnu.org/software/binutils/'
license=(GPL)
depends=(zlib)
source=(ftp://ftp.gnu.org/gnu/binutils/binutils-$pkgver.tar.bz2{,.sig}
        #binutils-${pkgver}-roundup-${_commit}.patch::https://sourceware.org/git/gitweb.cgi\?p=binutils-gdb.git\;a=commitdiff_plain\;h=${_commit}\;hp=binutils-2_25
        git+https://github.com/jcmvbkbc/crosstool-NG.git#branch=lx106-g++-1.21.0)
sha1sums=('1d597ae063e3947a5f61e23ceda8aebf78405fcd'
          'SKIP'
          'SKIP')
validpgpkeys=('EAF1C276A747E9ED86210CBAC3126D3B4AE55E93')  # Tristan Gingold <gingold@adacore.com>

prepare() {
  cd binutils-$pkgver
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
  #patch -p1 < ../binutils-$pkgver-roundup-$_commit.patch
  # Apply crosstool patches
  _patches=`ls ../crosstool-NG/local-patches/binutils/2.25/*.patch`
  for _patch in $_patches
  do
	  echo Applying $_patch
	  patch -p1 < $_patch
  done
  tar --strip-components=1 -xv -f ../crosstool-NG/overlays/xtensa_lx106.tar binutils
}

build() {
  cd binutils-$pkgver

  ./configure --target=$_target \
              --with-sysroot=/usr/$_target \
              --prefix=/usr \
              --disable-werror \
              --enable-ld=yes \
              --enable-gold=no \
              --disable-multilib \
              --disable-nls \

  make
}

check() {
  cd binutils-$pkgver
  
  # unset LDFLAGS as testsuite makes assumptions about which ones are active
  # do not abort on errors - manually check log files
  make LDFLAGS="" -k check
}

package() {
  cd binutils-$pkgver

  make DESTDIR="$pkgdir" install

  # Remove file conflicting with host binutils and manpages for MS Windows tools
  rm "$pkgdir"/usr/share/man/man1/xtensa-lx106-elf-{dlltool,nlmconv,windres,windmc}*

  # Remove info documents that conflict with host version
  rm -rf "$pkgdir"/usr/share/info
}
